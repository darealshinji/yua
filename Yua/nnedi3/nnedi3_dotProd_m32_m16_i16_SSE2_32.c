void dotProd_m32_m16_i16_SSE2(float *dataf, float *weightsf, float *vals, int n, int len, float *istd) {
	__asm__ {
		push edi
		push esi
		push ebx
		mov edi,weightsf
		mov eax,vals
		mov ebx,n
		mov esi,len
nloop:
		mov ecx,dataf
		pxor xmm0,xmm0
		pxor xmm1,xmm1
		pxor xmm2,xmm2
		pxor xmm3,xmm3
		mov edx,esi
lloop:
		movdqa xmm4,[ecx]
		movdqa xmm5,xmm4
		movdqa xmm6,xmm4
		movdqa xmm7,xmm4
		pmaddwd xmm4,[edi]
		pmaddwd xmm5,[edi+16]
		pmaddwd xmm6,[edi+32]
		pmaddwd xmm7,[edi+48]
		paddd xmm0,xmm4
		paddd xmm1,xmm5
		paddd xmm2,xmm6
		paddd xmm3,xmm7
		movdqa xmm4,[ecx+16]
		movdqa xmm5,xmm4
		movdqa xmm6,xmm4
		movdqa xmm7,xmm4
		pmaddwd xmm4,[edi+64]
		pmaddwd xmm5,[edi+80]
		pmaddwd xmm6,[edi+96]
		pmaddwd xmm7,[edi+112]
		paddd xmm0,xmm4
		paddd xmm1,xmm5
		paddd xmm2,xmm6
		paddd xmm3,xmm7
		movdqa xmm4,[ecx+32]
		movdqa xmm5,xmm4
		movdqa xmm6,xmm4
		movdqa xmm7,xmm4
		pmaddwd xmm4,[edi+128]
		pmaddwd xmm5,[edi+144]
		pmaddwd xmm6,[edi+160]
		pmaddwd xmm7,[edi+176]
		paddd xmm0,xmm4
		paddd xmm1,xmm5
		paddd xmm2,xmm6
		paddd xmm3,xmm7
		movdqa xmm4,[ecx+48]
		movdqa xmm5,xmm4
		movdqa xmm6,xmm4
		movdqa xmm7,xmm4
		pmaddwd xmm4,[edi+192]
		pmaddwd xmm5,[edi+208]
		pmaddwd xmm6,[edi+224]
		pmaddwd xmm7,[edi+240]
		paddd xmm0,xmm4
		paddd xmm1,xmm5
		paddd xmm2,xmm6
		paddd xmm3,xmm7
		add ecx,64
		add edi,256
		sub edx,32
		jnz lloop
		movdqa xmm4,xmm0
		movdqa xmm5,xmm2
		punpcklqdq xmm0,xmm1
		punpcklqdq xmm2,xmm3
		punpckhqdq xmm4,xmm1
		punpckhqdq xmm5,xmm3
		paddd xmm0,xmm4
		paddd xmm2,xmm5
		movdqa xmm6,xmm0
		shufps xmm0,xmm2,136
		shufps xmm6,xmm2,221
		paddd xmm6,xmm0
		movdqa [eax],xmm6
		add eax,16
		sub ebx,4
		jnz nloop
		mov ecx,istd
		mov eax,vals
		movss xmm7,[ecx]
		mov edx,n
		pshufd xmm7,xmm7,0
		xor ecx,ecx
aloop:
		movdqa xmm0,[eax+ecx*4]
		movdqa xmm1,[eax+ecx*4+16]
		movdqa xmm2,[eax+ecx*4+32]
		movdqa xmm3,[eax+ecx*4+48]
		cvtdq2ps xmm0,xmm0
		cvtdq2ps xmm1,xmm1
		cvtdq2ps xmm2,xmm2
		cvtdq2ps xmm3,xmm3
		mulps xmm0,[edi+ecx*8]
		mulps xmm1,[edi+ecx*8+32]
		mulps xmm2,[edi+ecx*8+64]
		mulps xmm3,[edi+ecx*8+96]
		mulps xmm0,xmm7
		mulps xmm1,xmm7
		mulps xmm2,xmm7
		mulps xmm3,xmm7
		addps xmm0,[edi+ecx*8+16]
		addps xmm1,[edi+ecx*8+48]
		addps xmm2,[edi+ecx*8+80]
		addps xmm3,[edi+ecx*8+112]
		movaps [eax+ecx*4],xmm0
		movaps [eax+ecx*4+16],xmm1
		movaps [eax+ecx*4+32],xmm2
		movaps [eax+ecx*4+48],xmm3
		add ecx,16
		sub edx,16
		jnz aloop
		pop ebx
		pop esi
		pop edi
	}
}
