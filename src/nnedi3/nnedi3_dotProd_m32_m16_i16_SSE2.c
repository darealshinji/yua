void dotProd_m32_m16_i16_SSE2(const float *dataf, const float *weightsf, float *vals, const int n, const int len, const float *istd) {
	__asm__ {
//Registers %rbp, %rbx and %r12 through %r15 "belong" to the calling function
		push rbx

		mov rdi,weightsf
		mov rax,vals
                mov ebx,n
                mov esi,len
nloop:
		mov rcx,dataf
		pxor xmm0,xmm0
		pxor xmm1,xmm1
		pxor xmm2,xmm2
		pxor xmm3,xmm3
		mov rdx,rsi
lloop:
		movdqa xmm4,[rcx]
		movdqa xmm5,xmm4
		movdqa xmm6,xmm4
		movdqa xmm7,xmm4
		pmaddwd xmm4,[rdi]
		pmaddwd xmm5,[rdi+16]
		pmaddwd xmm6,[rdi+32]
		pmaddwd xmm7,[rdi+48]
		paddd xmm0,xmm4
		paddd xmm1,xmm5
		paddd xmm2,xmm6
		paddd xmm3,xmm7
		movdqa xmm4,[rcx+16]
		movdqa xmm5,xmm4
		movdqa xmm6,xmm4
		movdqa xmm7,xmm4
		pmaddwd xmm4,[rdi+64]
		pmaddwd xmm5,[rdi+80]
		pmaddwd xmm6,[rdi+96]
		pmaddwd xmm7,[rdi+112]
		paddd xmm0,xmm4
		paddd xmm1,xmm5
		paddd xmm2,xmm6
		paddd xmm3,xmm7
		movdqa xmm4,[rcx+32]
		movdqa xmm5,xmm4
		movdqa xmm6,xmm4
		movdqa xmm7,xmm4
		pmaddwd xmm4,[rdi+128]
		pmaddwd xmm5,[rdi+144]
		pmaddwd xmm6,[rdi+160]
		pmaddwd xmm7,[rdi+176]
		paddd xmm0,xmm4
		paddd xmm1,xmm5
		paddd xmm2,xmm6
		paddd xmm3,xmm7
		movdqa xmm4,[rcx+48]
		movdqa xmm5,xmm4
		movdqa xmm6,xmm4
		movdqa xmm7,xmm4
		pmaddwd xmm4,[rdi+192]
		pmaddwd xmm5,[rdi+208]
		pmaddwd xmm6,[rdi+224]
		pmaddwd xmm7,[rdi+240]
		paddd xmm0,xmm4
		paddd xmm1,xmm5
		paddd xmm2,xmm6
		paddd xmm3,xmm7
		add rcx,64
		add rdi,256
		sub rdx,32
		jnz lloop
		movdqa xmm4,xmm0
		movdqa xmm5,xmm2
		punpcklqdq xmm0,xmm1
		punpcklqdq xmm2,xmm3
		punpckhqdq xmm4,xmm1
		punpckhqdq xmm5,xmm3
		paddd xmm0,xmm4
		paddd xmm2,xmm5
		movdqa xmm6,xmm0
		shufps xmm0,xmm2,136
		shufps xmm6,xmm2,221
		paddd xmm6,xmm0
		movdqa [rax],xmm6
		add rax,16
		sub rbx,4
		jnz nloop
		mov rcx,istd
		mov rax,vals
		movss xmm7,[rcx]
                mov edx,n
		pshufd xmm7,xmm7,0
		xor rcx,rcx
aloop:
		movdqa xmm0,[rax+rcx*4]
		movdqa xmm1,[rax+rcx*4+16]
		movdqa xmm2,[rax+rcx*4+32]
		movdqa xmm3,[rax+rcx*4+48]
		cvtdq2ps xmm0,xmm0
		cvtdq2ps xmm1,xmm1
		cvtdq2ps xmm2,xmm2
		cvtdq2ps xmm3,xmm3
		mulps xmm0,[rdi+rcx*8]
		mulps xmm1,[rdi+rcx*8+32]
		mulps xmm2,[rdi+rcx*8+64]
		mulps xmm3,[rdi+rcx*8+96]
		mulps xmm0,xmm7
		mulps xmm1,xmm7
		mulps xmm2,xmm7
		mulps xmm3,xmm7
		addps xmm0,[rdi+rcx*8+16]
		addps xmm1,[rdi+rcx*8+48]
		addps xmm2,[rdi+rcx*8+80]
		addps xmm3,[rdi+rcx*8+112]
		movaps [rax+rcx*4],xmm0
		movaps [rax+rcx*4+16],xmm1
		movaps [rax+rcx*4+32],xmm2
		movaps [rax+rcx*4+48],xmm3
		add rcx,16
		sub rdx,16
		jnz aloop
		
		pop rbx
	}
}
